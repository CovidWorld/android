apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

def props = new Properties()
props.load(new FileInputStream(project.file('app.properties')))

android {
    signingConfigs {
        release {
            storeFile file(props['keystoreFile'])
            storePassword props['keystorePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId 'covid19'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0.0'
        manifestPlaceholders.put('googleApiKey', props['googleApiKey'])
        buildConfigField('String', 'GOOGLE_API_KEY', '"' + props['googleApiKey'] + '"')
        buildConfigField('java.util.UUID', 'BEACON_UUID', 'java.util.UUID.fromString("' + props['beaconUuid'] + '")')
        resValue("string", "defaultApiUrl", props['apiUrl'])
    }
    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'country'
    productFlavors {
        global {
            dimension 'country'
        }
        slovakia {
            dimension 'country'
            applicationId 'sk.minv.covid19'
            versionNameSuffix '-sk'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.gms:play-services-nearby:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.libraries.places:places:2.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.firebase:firebase-config:19.1.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.github.ialokim:android-phone-field:0.2.3'
    implementation 'sk.turn:beacons-android:1.5.3'
    implementation 'sk.turn:http:1.7.0'
}
