apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

def props = new Properties()
props.load(new FileInputStream(project.file('app.properties')))

android {
    signingConfigs {
        release {
            storeFile file(props['keystoreFile'])
            storePassword props['keystorePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId 'int.covid19'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0.0'
        buildConfigField('java.util.UUID', 'BEACON_UUID', 'java.util.UUID.fromString("' + props['beaconUuid'] + '")')
        resValue("string", "defaultApiUrl", props['apiUrl'])
    }
    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'country'
    productFlavors {
        global {
            dimension 'country'
        }
        slovakia {
            dimension 'country'
            applicationId 'sk.nczi.covid19'
            versionCode 4
            versionName '1.1.0'
            versionNameSuffix '-sk'
            buildConfigField('String', 'PHONE_VERIFICATION_API_URL', '"' + props['phoneVerificationApiUrl'] + '"')
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.gms:play-services-nearby:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.libraries.places:places:2.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-messaging:20.1.4'
    implementation 'com.google.firebase:firebase-config:19.1.3'
    implementation 'com.google.maps.android:android-maps-utils:1.0.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.github.ialokim:android-phone-field:0.2.3'
    implementation 'sk.turn:beacons-android:1.5.4'
    implementation 'sk.turn:http:1.7.0'
    implementation 'org.hashids:hashids:1.0.3'
    compileOnly 'com.innovatrics.android:dot:2.15.1'
    // Only include the below dependency for flavors where CountryDefaults.useFaceId() returns true
    //globalImplementation 'com.innovatrics.android:dot:2.15.1'
    implementation 'com.auth0.android:jwtdecode:2.0.0'
}
